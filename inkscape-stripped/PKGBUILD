# Maintainer: mrdotx <klassiker@gmx.de>
_pkgname=inkscape
pkgname=inkscape-stripped
pkgdesc='Professional vector graphics editor (stripped from most unnecessary dependencies and extensions)'
pkgver=1.4.3
pkgrel=1
url='https://inkscape.org'
arch=('x86_64')
license=('GPL-2.0-or-later')
makedepends=(
    'cmake'
    'boost'
    'git'
    'glib2-devel'
)
depends=(
    'atkmm'
    'boost-libs'
    'cairo'
    'cairomm'
    'double-conversion'
    'fontconfig'
    'freetype2'
    'gc'
    'gcc-libs'
    'gdk-pixbuf2'
    'glib2'
    'glibc'
    'glibmm'
    #'gobject-introspection-runtime'
    'graphicsmagick'
    'gsl'
    'gspell'
    'gtk3'
    'gtkmm3'
    'gtksourceview4'
    'harfbuzz'
    'hicolor-icon-theme'
    'lcms2'
    'lib2geom'
    'libcdr'
    'libepoxy'
    'libjpeg-turbo'
    'libpng'
    'librevenge'
    'libsigc++'
    'libvisio'
    'libwpg'
    'libx11'
    'libxml2'
    'libxslt'
    #'pango'
    #'pangomm'
    #'poppler'
    'poppler-glib'
    'potrace'
    'python'
    #'python-appdirs'
    #'python-beautifulsoup4'
    #'python-cachecontrol'
    #'python-cairo'
    #'python-certifi'
    #'python-chardet'
    #'python-cssselect'
    #'python-filelock'
    #'python-gobject'
    #'python-idna'
    #'python-lockfile'
    'python-lxml'
    #'python-msgpack'
    'python-numpy'
    #'python-packaging'
    #'python-pillow'
    #'python-pyserial'
    #'python-requests'
    'python-tinycss2'
    #'python-urllib3'
    #'python-zstandard'
    'readline'
    #'scour'
    'ttf-font'
    'zlib'
)
optdepends=(
    'fig2dev: xfig input'
    'gvfs: import clip art'
    'pstoedit: latex formulas'
    # this pulls in texlive-basic, so should suffice
    'texlive-pstricks: latex formulas'
)
provides=("$_pkgname")
conflicts=("$_pkgname")
source=(
    "git+https://gitlab.com/inkscape/inkscape.git#tag=INKSCAPE_${pkgver//./_}"
    'inkscape-extensions::git+https://gitlab.com/inkscape/extensions.git'
    'inkscape-libcroco::git+https://gitlab.com/inkscape/libcroco.git'
    #'inkscape-themes::git+https://gitlab.com/inkscape/themes.git'
    #'inkscape-extras-extensions-gcodetools::git+https://gitlab.com/inkscape/extras/extensions-gcodetools.git'
    #'inkscape-extras-extension-manager::git+https://gitlab.com/inkscape/extras/extension-manager.git'
    #'inkscape-extras-inkscape-import-clipart::git+https://gitlab.com/inkscape/extras/inkscape-import-clipart.git'
    #'inkscape-extras-extension-xaml::git+https://gitlab.com/inkscape/extras/extension-xaml.git'
    #'inkscape-extras-extension-afdesign::git+https://gitlab.com/inkscape/extras/extension-afdesign.git'
    #'inkscape-extras-extension-curve::git+https://gitlab.com/inkscape/extras/extension-curve.git'
)
b2sums=('a92b1c9251e7d23d0ec05f7ff156760ec0c301c6220e6d1d98c915035c6b0ab0abe857ac9d0d4255753062979eeb3f88b44d1ddf456e181d532d093de4dac31b'
        'SKIP'
        'SKIP')

_backports=(
)

_reverts=(
)

prepare() {
    cd "$_pkgname"
    git submodule init
    git submodule set-url share/extensions ../inkscape-extensions/
    git submodule deinit -f src/3rdparty/2geom
    git submodule set-url src/3rdparty/libcroco ../inkscape-libcroco/
    #git submodule set-url share/themes ../inkscape-themes/
    git -c protocol.file.allow=always submodule update

    #(
    #cd share/extensions/
    #git submodule set-url other/gcodetools ../inkscape-extras-extensions-gcodetools/
    #git submodule set-url other/inkman ../inkscape-extras-extension-manager/
    #git submodule set-url other/clipart ../inkscape-extras-inkscape-import-clipart/
    #git submodule set-url other/extension-xaml ../inkscape-extras-extension-xaml/
    #git submodule set-url other/extension-afdesign ../inkscape-extras-extension-afdesign/
    #git submodule set-url other/extension-curve ../inkscape-extras-extension-curve/
    #git -c protocol.file.allow=always submodule update --init
    #)

    local _c _l
    for _c in "${_backports[@]}"; do
      if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
      git log --oneline "${_l}" "${_c}"
      git cherry-pick --mainline 1 --no-commit "${_c}"
    done
    for _c in "${_reverts[@]}"; do
      if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
      git log --oneline "${_l}" "${_c}"
      git revert --mainline 1 --no-commit "${_c}"
    done
}

build() {
    local cmake_options=(
        -B build
        -D CMAKE_BUILD_TYPE=None
        -D CMAKE_INSTALL_PREFIX:PATH=/usr
        -W no-dev
        -S $_pkgname
    )

    cmake "${cmake_options[@]}"
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --build build --target install
}
