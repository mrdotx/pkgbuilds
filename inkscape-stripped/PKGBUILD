# Maintainer: mrdotx <klassiker@gmx.de>
_pkgname=inkscape
pkgname=inkscape-stripped
pkgdesc='Professional vector graphics editor (stripped from most unnecessary dependencies and extensions)'
pkgver=1.4
pkgrel=7
url='https://inkscape.org'
arch=('x86_64')
license=('GPL-3.0-or-later')
makedepends=(
    'cmake'
    'boost'
    'git'
    'glib2-devel'
)
depends=(
    'atkmm'
    'boost-libs'
    'cairo'
    'cairomm'
    'double-conversion'
    'fontconfig'
    'freetype2'
    'gc'
    'gcc-libs'
    'gdk-pixbuf2'
    'glib2'
    'glibc'
    'glibmm'
    #'gobject-introspection-runtime'
    'graphicsmagick'
    'gsl'
    'gspell'
    'gtk3'
    'gtkmm3'
    'gtksourceview4'
    'harfbuzz'
    'hicolor-icon-theme'
    'lcms2'
    'lib2geom'
    'libcdr'
    'libepoxy'
    'libjpeg-turbo'
    'libpng'
    'librevenge'
    'libsigc++'
    'libsoup'
    'libvisio'
    'libwpg'
    'libx11'
    'libxml2'
    'libxslt'
    #'pango'
    #'pangomm'
    #'poppler'
    'poppler-glib'
    'potrace'
    'python'
    #'python-appdirs'
    #'python-beautifulsoup4'
    #'python-cachecontrol'
    #'python-cairo'
    #'python-certifi'
    #'python-chardet'
    #'python-coverage'
    #'python-cssselect'
    #'python-filelock'
    #'python-gobject'
    #'python-idna'
    #'python-lockfile'
    'python-lxml'
    #'python-msgpack'
    'python-numpy'
    #'python-packaging'
    #'python-pillow'
    #'python-pyserial'
    #'python-requests'
    #'python-urllib3'
    #'python-zstandard'
    'readline'
    #'scour'
    'ttf-font'
    'zlib'
)
optdepends=(
    'fig2dev: xfig input'
    'gvfs: import clip art'
    'pstoedit: latex formulas'
    # this pulls in texlive-basic, so should suffice
    'texlive-pstricks: latex formulas'
    'python-tinycss2: DXF export'
)
provides=("$_pkgname")
conflicts=("$_pkgname")
source=(
    "git+https://gitlab.com/inkscape/inkscape.git#tag=INKSCAPE_${pkgver//./_}"
    'inkscape-lib2geom::git+https://gitlab.com/inkscape/lib2geom.git'
    #'inkscape-themes::git+https://gitlab.com/inkscape/themes.git'
    'inkscape-extensions::git+https://gitlab.com/inkscape/extensions.git'
    #'inkscape-extras-inkscape-import-clipart::git+https://gitlab.com/inkscape/extras/inkscape-import-clipart.git'
    #'inkscape-extras-extension-xaml::git+https://gitlab.com/inkscape/extras/extension-xaml.git'
    #'inkscape-extras-extensions-gcodetools::git+https://gitlab.com/inkscape/extras/extensions-gcodetools.git'
    #'inkscape-extras-extension-manager::git+https://gitlab.com/inkscape/extras/extension-manager.git'
)
b2sums=('7b3c989ab2251f7c3a6a4b94ebb1cd3e5d1b834d5f286431599e56feb1fcc9f6cb72f11cb72a3ab6d028e2bf2c627f47d74c33d2e7c5ec9fd3d9aaa80b4b4884'
        'SKIP'
        'SKIP')

_backports=(
    # Future-proof against poppler 24.10 changes
    '22304ae8034d067670a9f95022083a75fac92b4c'
    # Fix building with Poppler 24.11
    '0399372ec240d23e0e70548237a541f2b5bf0f34'
    # Fix Poppler private includes
    '874dcfbd303bc7a1bddb6f34aebbb4dba8eda771'
    # Fix building with poppler 24.12.0
    'c9046810d899a408bfbd489aad91872b1203ee6d'
    # Fix building with poppler 25.02.0
    '9754274ea0ee76d9888ec0b306885ecba6926702'
)

_reverts=(
)

prepare() {
    cd "$_pkgname"

    git submodule set-url share/extensions ../inkscape-extensions/
    git submodule set-url src/3rdparty/2geom ../inkscape-lib2geom/
    #git submodule set-url share/themes ../inkscape-themes/
    git -c protocol.file.allow=always submodule update --init

    #(
    #cd share/extensions/
    #git submodule set-url other/gcodetools ../inkscape-extras-extensions-gcodetools/
    #git submodule set-url other/inkman ../inkscape-extras-extension-manager/
    #git submodule set-url other/clipart ../inkscape-extras-inkscape-import-clipart/
    #git submodule set-url other/extension-xaml ../inkscape-extras-extension-xaml/
    #git -c protocol.file.allow=always submodule update --init
    #)

    local _c _l
    for _c in "${_backports[@]}"; do
      if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
      git log --oneline "${_l}" "${_c}"
      git cherry-pick --mainline 1 --no-commit "${_c}"
    done
    for _c in "${_reverts[@]}"; do
      if [[ "${_c}" == *..* ]]; then _l='--reverse'; else _l='--max-count=1'; fi
      git log --oneline "${_l}" "${_c}"
      git revert --mainline 1 --no-commit "${_c}"
    done
}

build() {
    local cmake_options=(
        -B build
        -D CMAKE_BUILD_TYPE=None
        -D CMAKE_INSTALL_PREFIX:PATH=/usr
        -W no-dev
        -S $_pkgname
    )

    cmake "${cmake_options[@]}"
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --build build --target install
}
